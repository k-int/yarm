buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.9.3"
        classpath "org.grails.plugins:hibernate5:6.0.2"
    }
}

version "0.1"
group "yarm_app"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repo.grails.org/grails/plugins/" }
    maven { url "http://nexus.k-int.com/content/repositories/releases" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.1.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.1.Final"
    console "org.grails:grails-console"
    compile 'org.grails.plugins:quartz:2.0.9'
    compile 'org.grails.plugins:spring-security-core:3.1.+'
    compile 'org.grails.plugins:spring-security-oauth2:1.+'
    compile 'org.grails.plugins:spring-security-oauth2-google:1.1.+'
    compile 'com.k-int:goai:1.0.2'


    // compile "org.grails.plugins:slug-generator:0.5"

    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.9.3"
    runtime "com.h2database:h2"
    runtime  group: 'postgresql', name: 'postgresql', version: '9.1-901-1.jdbc4'
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    println "Looking for yarm_subprojects... ${parent?.subprojects}"
    def gtp = parent?.subprojects?.findAll {
      if (["yarm_dm"].contains(it.name) ) {
        compile it
        println "\tfound locally at ${it.projectDir}"
        return true
      }

      false
    }

    if (!gtp) {
      println "\tno local copy found downloading from Maven"
      compile 'com.k-int.yarm:yarm_dm:0.1'
    }

}


assets {
    minifyJs = true
    minifyCss = true
    includes = ["fonts/*"]
    // configOptions = [commonJs: false] //useful for custom config on extension libraries
    // excludes = [ 'select2/js/*' ]
}
